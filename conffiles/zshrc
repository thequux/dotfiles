# -*- mode: sh; sh-shell-file: /bin/zsh; sh-basic-offset: 8 -*-
# The following lines were added by compinstall

if [[ "$TERM" == "dumb" ]]; then
	unsetopt prompt_cr prompt_sp
	return
fi

zstyle ':completion:*' completer _expand _complete
zstyle ':completion:*' accept-exact-dirs true
zstyle ':completion:*' completions 1
zstyle ':completion:*' glob 1
zstyle ':completion:*' substitute 1
zstyle :compinstall filename "$HOME/.../conffiles/zshrc"

autoload -Uz compinit
compinit
# End of lines added by compinstall
# Lines configured by zsh-newuser-install
HISTFILE=~/.histfile
HISTSIZE=100000
SAVEHIST=100000
setopt appendhistory autocd HIST_IGNORE_DUPS INTERACTIVE_COMMENTS extendedglob
unsetopt beep nomatch bang_hist
setopt extendedglob prompt_subst

bindkey -e
# End of lines configured by zsh-newuser-install

# We depend on .zshenv having been run
_tq_update_envfile_ts "${HOME}/.zshrc"

typeset -A env_name

ZENV=$HOME/.../conffiles/zenv

function env- () {
	if (( ${env_name[$1]-0} > 0 )); then
		if (( --env_name[$1] <= 0 )); then
			unset env_name[$1]
			if [[ -x "${ZENV}/$1" ]]; then
				source "${ZENV}/$1" disable
			fi
		fi
	fi
}	

function env+ () {
	if (( ++env_name[$1] == 1 )); then
		if [[ -x "${ZENV}/$1" ]]; then
			source "${ZENV}/$1" enable
		fi
	fi
}

function 'env?'() {
	for x in ${ZENV}/*; do
		if [[ $x:t =~ ".*~" ]]; then
			continue
		fi
		[[ -x $x ]] && printf "%s\t%s\n" ${x:t} "$(sed -ne '1s/^# */- /p' "$x")" || true
	done
}

function reinit() {
	for x in "${(ok@)_tq_envfile_ts}"; do
		echo "$x"
		. "$x"
	done
}

function infostr() {
	local tagstart tagend msgstart msgend
	if (( 1 - $SIMPLE_PROMPT )); then
		tagstart=$'%{\e[33m%}'
		tagend=$'%{\e[0m%}'
		msgstart=$'%{\e[1;33m%}'
		msgend=$'%{\e[0m%}'
	fi
	printf "(%s%s%s:%s%s%s)" "$tagstart" "$1" "$tagend" "$msgstart" "$2" "$msgend"
}

function xtitle() {
	printf "\e]0;%s\a" "$*"
}

function extra_info() {
	if [[ -d $(printf "%s" ./(../)#.git/(#qY1)) || -n $GIT_DIR ]]; then
		infostr git "$(git name-rev --always --name-only HEAD)" 
	fi

	if [[ -f /.chroot-name ]]; then
		infostr chroot "$(</.chroot-name)"
	fi

	if [[ ${#env_name} -gt 0 ]]; then
		infostr env "$(print -o "${(kO@)env_name}")"
	fi
	(( ${+VIRTUAL_ENV} )) && infostr virtualenv "${VIRTUAL_ENV:t}"
	(( ${+RUBY_VERSION} )) && infostr ruby "${RUBY_VERSION}"
	typeset -A needs_updated
	for envfile ts in "${(vk)_tq_envfile_ts[@]}"; do
		if [[ ${ts} -lt $( stat -Lc "%Y" "$envfile" ) ]]; then
			needs_updated[$envfile]=y
		fi
	done
	if (( ${#needs_updated} )); then
		infostr "old cfg" "`print ${(oDk@)needs_updated}`"
	fi
}

# if I'm in emacs, certain things should be changed...
if [[ -z "${INSIDE_EMACS}" ]]; then
   SIMPLE_PROMPT=0
   PROMPT=$'%{\e[1;32m%}%n%{\e[0;39m%}@%{\e[1;31m%}%m%{\e[0;39m%} <%{\e[1;36m%}%~%{\e[0;39m%}> $(extra_info)\n%{\e[%(#.31.1)m%}[%?]%{\e[0m%}$ '
   export EDITOR=emacsclient
   export ALTERNATE_EDITOR=""
else
    SIMPLE_PROMPT=1
    PROMPT=$'%n@%m <%~> $(extra_info)\n[%?] '
    alias ls='ls --color=never'
fi

export LD_LIBRARY_PATH="$HOME/lib:$HOME/local/lib${LD_LIBRARY_PATH:+:${LD_LIBRARY_PATH}}"

case $(hostname) in
	vulcan)
		export INFOPATH=/usr/share/info/emacs-23:
		;|
esac

if [[ "$TERM" == vt100 ]]; then
	setopt NO_PROMPT_SP
fi

setopt extended_history
eval $(perl -I$HOME/local/perl5/lib/perl5 -Mlocal::lib=$HOME/local/perl5)

function e() { "${EDITOR:-editor}" "$@"; }
alias ls="ls --color=auto"
eval "`dircolors ~/.dircolors`"
alias td="todo2 --file='$HOME/.todo2'"
function lastcmd() {
	echo "${history[$((${#history}))]}"
}

VIRTUAL_ENV_DISABLE_PROMPT=1
[[ -f /etc/bash_completion.d/virtualenvwrapper ]] && source /etc/bash_completion.d/virtualenvwrapper
eval `perl -Mlocal::lib=$HOME/local`

###-begin-npm-completion-###
#
# npm command completion script
#
# Installation: npm completion >> ~/.bashrc  (or ~/.zshrc)
# Or, maybe: npm completion > /usr/local/etc/bash_completion.d/npm
#

COMP_WORDBREAKS=${COMP_WORDBREAKS/=/}
COMP_WORDBREAKS=${COMP_WORDBREAKS/@/}
export COMP_WORDBREAKS

if type complete &>/dev/null; then
  _npm_completion () {
    local si="$IFS"
    IFS=$'\n' COMPREPLY=($(COMP_CWORD="$COMP_CWORD" \
                           COMP_LINE="$COMP_LINE" \
                           COMP_POINT="$COMP_POINT" \
                           npm completion -- "${COMP_WORDS[@]}" \
                           2>/dev/null)) || return $?
    IFS="$si"
  }
  complete -F _npm_completion npm
elif type compdef &>/dev/null; then
  _npm_completion() {
    si=$IFS
    compadd -- $(COMP_CWORD=$((CURRENT-1)) \
                 COMP_LINE=$BUFFER \
                 COMP_POINT=0 \
                 npm completion -- "${words[@]}" \
                 2>/dev/null)
    IFS=$si
  }
  compdef _npm_completion npm
elif type compctl &>/dev/null; then
  _npm_completion () {
    local cword line point words si
    read -Ac words
    read -cn cword
    let cword-=1
    read -l line
    read -ln point
    si="$IFS"
    IFS=$'\n' reply=($(COMP_CWORD="$cword" \
                       COMP_LINE="$line" \
                       COMP_POINT="$point" \
                       npm completion -- "${words[@]}" \
                       2>/dev/null)) || return $?
    IFS="$si"
  }
  compctl -K _npm_completion npm
fi
###-end-npm-completion-###

# OPAM configuration
. $HOME/.opam/opam-init/init.zsh > /dev/null 2> /dev/null || true
