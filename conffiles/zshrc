# -*- mode: sh; sh-shell-file: /bin/zsh -*-
# The following lines were added by compinstall

if [[ "$TERM" == "dumb" ]]; then
	unsetopt prompt_cr prompt_sp
	return
fi

zstyle ':completion:*' completer _expand _complete
zstyle ':completion:*' accept-exact-dirs true
zstyle ':completion:*' completions 1
zstyle ':completion:*' glob 1
zstyle ':completion:*' substitute 1
zstyle :compinstall filename "$HOME/.../conffiles/zshrc"

autoload -Uz compinit
compinit
# End of lines added by compinstall
# Lines configured by zsh-newuser-install
HISTFILE=~/.histfile
HISTSIZE=100000
SAVEHIST=100000
setopt appendhistory autocd HIST_IGNORE_DUPS INTERACTIVE_COMMENTS extendedglob
unsetopt beep nomatch bang_hist
setopt extendedglob prompt_subst

bindkey -e
# End of lines configured by zsh-newuser-install

typeset -A env_name

function env- () {
	(( --env_name[$1] > 0 )) || unset env_name[$1]
}	

function env+ () {
	# Work even with -e
	: $(( ++env_name[$1] ))
}

function infostr() {
	local tagstart tagend msgstart msgend
	if (( 1 - $SIMPLE_PROMPT )); then
		tagstart=$'%{\e[33m%}'
		tagend=$'%{\e[0m%}'
		msgstart=$'%{\e[1;33m%}'
		msgend=$'%{\e[0m%}'
	fi
	printf "(%s%s%s:%s%s%s)" "$tagstart" "$1" "$tagend" "$msgstart" "$2" "$msgend"
}

function extra_info() {
	if [[ -d $(printf "%s" ./(../)#.git/) || -n $GIT_DIR ]]; then
		infostr git "$(git name-rev --always --name-only HEAD)" 
	fi

	if [[ -f /.chroot-name ]]; then
		infostr chroot "$(</.chroot-name)"
	fi

	if [[ ${#env_name} -gt 0 ]]; then
		infostr env "$(print -o "${(kO@)env_name}")"
	fi
	if [[ ${+VIRTUAL_ENV} -ne 0 ]]; then
		infostr virtualenv "${VIRTUAL_ENV:t}"
	fi
}

# if I'm in emacs, certain things should be changed...
if [[ -z "${INSIDE_EMACS}" ]]; then
   SIMPLE_PROMPT=0
   PROMPT=$'%{\e[1;32m%}%n%{\e[0;39m%}@%{\e[1;31m%}%m%{\e[0;39m%} <%{\e[1;36m%}%~%{\e[0;39m%}> $(extra_info)\n%{\e[%(#.31.1)m%}[%?]%{\e[0m%}$ '
   export EDITOR=emacsclient
   export ALTERNATE_EDITOR=""
else
    SIMPLE_PROMPT=1
    PROMPT=$'%n@%m <%~> $(extra_info)\n[%?] '
    alias ls='ls --color=never'
fi

export LD_LIBRARY_PATH="$HOME/lib:$HOME/local/lib${LD_LIBRARY_PATH:+:${LD_LIBRARY_PATH}}"

case $(hostname) in
	vulcan)
		export INFOPATH=/usr/share/info/emacs-23:
		;|
esac

if [[ "$TERM" == vt100 ]]; then
	setopt NO_PROMPT_SP
fi

setopt extended_history
eval $(perl -I/home/thequux/local/perl5/lib/perl5 -Mlocal::lib=/home/thequux/local/perl5)

function e() { "${EDITOR:-editor}" "$@"; }
alias ls="ls --color=auto"
eval "`dircolors ~/.dircolors`"
alias td="todo2 --file='$HOME/.todo2'"
function lastcmd() {
	echo "${history[$((${#history}))]}"
}

VIRTUAL_ENV_DISABLE_PROMPT=1
[[ -f /etc/bash_completion.d/virtualenvwrapper ]] && source /etc/bash_completion.d/virtualenvwrapper
eval `perl -Mlocal::lib=$HOME/local`
